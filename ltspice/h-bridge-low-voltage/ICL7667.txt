**********
* Copyright Intusoft 1994-1997
* All Rights Reserved
**********
**********
*SRC=ICL7667;ICL7667;Drivers;Power Mosfet;TTL/CMOS Input
*SYM=ICL7667
*DWG=ICL7667S.DWG
.SUBCKT ICL7667  3  8   1   2	4	6
*Connections     In Vdd Out Vss in2 out2
I1 8 4 2M
X1 4 6 8 2 ICL
X2 6 1 8 2 ICL
R1 4 2 1MEG
M1 4 3 2 2 INPUT
.MODEL INPUT NMOS (LEVEL=1 VTO=0.80 KP=1M GAMMA=2U
+ PHI=0.75 LAMBDA=14M RD=30 RS=36 IS=11.2F CBD=5.13P 
+ CBS=6.16P PB=0.80 MJ=.46 CGSO=3.60N CGDO=3.00N
+ CGBO=23.4N)
.ENDS
.SUBCKT ICL 6  1   4   3
*Connections     In Out Vdd Vss
M2 1 6 3 3 NMOS L=1U W=1U
M3 1 6 4 4 PMOS L=1U W=1U
.MODEL NMOS NMOS (LEVEL=1 VTO=1.9 KP=13.2M GAMMA=.62
+ PHI=.75 LAMBDA=4.16M RD=1.4 RS=1.4 IS=5F PB=.8
+ MJ=.46 CBD=2.7P CBS=2.57P CGSO=3.9N CGDO=3.25N
+ CGBO=5.35N)
.MODEL PMOS PMOS (LEVEL=1 VTO=-2.1 KP=7.75M GAMMA=.62
+ PHI=.75 LAMBDA=4.16M RD=1.4 RS=1.4 IS=5F PB=.8
+ MJ=.46 CBD=2.7P CBS=2.57P CGSO=3.9N CGDO=3.25N
+ CGBO=5.35N)
.ENDS
**********
